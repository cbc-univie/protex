*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*~ constant pressure production run
*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*

if @?INDEX eq 0 set INDEX = 1
calc PREV = @INDEX - 1

ioformat extended
!=======================================================================
! Force field
!=======================================================================
read rtf  card name "../non_polarizable_wB97xD.rtf"
read para card name "../non_polarizable.prm"
read psf  card name  "./psf/im1h_oac_500_@PREV.psf"

!=======================================================================
! Coordinates
!=======================================================================
read coor card name "./im1h_oac_500_python_pH.crd"

set XTL = 48.0
calc XTL2 = @xtl/2
set GRID = 48   

crystal define cubic @XTL @XTL @XTL 90.0 90.0 90.0
crystal build  cutoff @XTL2 noperations 0
image byresidue select ALL end

energy  bycb atom vatom cdiel ewal pmew vswitch -
        ctonnb           10  -
        ctofnb           11  -
        cutnb            14  -
        cutim            14  -
        kappa           0.41 -
        fftx          @GRID  -
        ffty          @GRID  -
        fftz          @GRID  -
        spli order        6  -
        qcor              0  -
        wmin            1.5  -
        inbfrq           -1  -
        imgfrq           -1

shake bonh param tol 1.0e-9 nofast

!=======================================================================
! Dynamics
!=======================================================================
if @?NSTEP eq 0 set NSTEP = 1000 
if @?DT    eq 0 set DT    = 0.001 ! in ps
if @?TEMP  eq 0 set TEMP = 300.0

if @INDEX eq 1 then
  set STARTER = START
  set IUNREA  = -1
else
  set STARTER = RESTART
  set IUNREA  = 15
  open unit 15 read form name ./traj/im1h_oac_500_@PREV.rst
  stream str/current_charges_@PREV.str
endif  
write psf card name  "./psf/im1h_oac_500_@INDEX.psf"

! current trajectory file
open unit 16 write form name ./traj/im1h_oac_500_@INDEX.rst
open unit 17 write file name ./traj/im1h_oac_500_@INDEX.dcd
dyna cpt leap  @starter  -
     ISEED        14 09 19 80 -
     NSTEP        @nstep  -
     TIMESTEP        @dt  -
     IPRFRQ         1000  -
     IMGFRQ           -1  -
     INBFRQ           -1  -
     NTRFRQ         5000  -
     NPRINT          100  -
     NSAVC            50  -
     NSAVV            -1  -
     IUNREA      @iunrea  -
     IUNWRI           16  -
     IUNCRD           17  -
     IUNVEL           -1  -
     HOOVER               -
     TBATH          @temp -
     TMASS         1000.0 -
     PCONST               -
     PREF             1.0 -
     PMASS         1000.0 -
     PGAMMA          20.0 -
     ECHECK            -1

stop
